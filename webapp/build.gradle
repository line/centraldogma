plugins {
    id 'base'
    alias(libs.plugins.node.gradle)
}

node {
    version = '22.3.0'
    npmVersion = '10.8.1'
    download = true
    npmInstallCommand = "ci"
}

dependencies {
    // Dependencies to run test servers for testing the web application
    testImplementation(project(":server"))
    testImplementation(project(":server-mirror-git"))
    testImplementation(project(":server-auth:saml"))
    testImplementation(project(":server-auth:shiro"))
    testImplementation(project(":xds"))
    testImplementation libs.shiro.core
}

sourceSets {
    test {
        java {
            srcDir 'javaTest/java'
        }
        resources {
            srcDir 'javaTest/resources'
        }
    }
}

task installPlayWright(type: NpmTask) {
    dependsOn tasks.npmInstall
    args = ['run', 'playwright:install']
}

// Set `NEXT_ENV=development` to `.env.local` file to produce source maps for the minified JavaScript files.
task buildWeb(type: NpmTask) {
    dependsOn tasks.npmInstall
    dependsOn installPlayWright
    args = ['run', 'build']
    inputs.dir('src')
    inputs.file('package.json')
    inputs.file('package-lock.json')
    inputs.file('next.config.js')

    outputs.dir('build/web')
}

task copyWeb(type: Copy) {
    dependsOn buildWeb

    from 'build/web'
    into 'build/javaweb/com/linecorp/centraldogma/webapp'
}

task runTestServer(type: JavaExec) {
    group = "Execution"
    description = "Run the simple Central Dogma server"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "com.linecorp.centraldogma.webapp.SimpleCentralDogmaTestServer"
}

task runTestShiroServer(type: JavaExec) {
    group = "Execution"
    description = "Run the Central Dogma server with Apache Shiro"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "com.linecorp.centraldogma.webapp.ShiroCentralDogmaTestServer"
}

if (!rootProject.hasProperty('noLint')) {
    task eslint(type: NpmTask) {
        dependsOn tasks.npmInstall

        args = ['run', 'lint']

        inputs.dir('src')
        inputs.file('package.json')
        inputs.file('package-lock.json')
        inputs.file('next.config.js')
        outputs.upToDateWhen { true }
    }

    task prettier(type: NpmTask) {
        dependsOn tasks.eslint

        args = ['run', 'format']

        inputs.dir('src')
        inputs.file('package.json')
        inputs.file('package-lock.json')
        inputs.file('next.config.js')
        outputs.upToDateWhen { true }
    }

    Task lintTask = project.ext.getLintTask()
    lintTask.dependsOn(tasks.prettier)
    tasks.buildWeb.dependsOn(tasks.prettier)
}

task testWeb(type: NpmTask) {
    dependsOn tasks.npmInstall

    args = ['run', 'test:ci']

    inputs.dir('src')
    inputs.file('package.json')
    inputs.file('package-lock.json')
    inputs.file('next.config.js')
    outputs.upToDateWhen { true }
}

task testE2e(type: NpmTask) {
    dependsOn tasks.npmInstall

    args = ['run', 'test:e2e']

    inputs.dir('src')
    inputs.file('package.json')
    inputs.file('package-lock.json')
    inputs.file('playwright.config.ts')
    outputs.upToDateWhen { true }
}
tasks.test.dependsOn(tasks.testWeb)
tasks.test.dependsOn(tasks.testE2e)

tasks.withType(Test).configureEach {
    failOnNoDiscoveredTests = false
}
