buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:3.4.0"
    }
}

apply plugin: 'com.github.node-gradle.node'

node {
    version = '16.14.0'
    npmVersion = '8.5.2'
    download = true
    npmInstallCommand = "ci"

    // Change the cache location under Gradle user home directory so that it's cached by CI.
    if (System.getenv('CI') != null) {
        workDir = file("${gradle.gradleUserHomeDir}/caches/nodejs/${project.name}")
        npmWorkDir = file("${gradle.gradleUserHomeDir}/caches/npm/${project.name}")
    }
}

dependencies {
    implementation project(':common-legacy')

    // Armeria
    ['armeria', 'armeria-thrift0.9'].each {
        api "com.linecorp.armeria:$it"
    }
    testImplementation 'com.linecorp.armeria:armeria-junit5'

    // JSch
    implementation 'com.jcraft:jsch'

    // Caffeine
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // Curator & ZooKeeper
    implementation 'org.apache.curator:curator-recipes'
    implementation 'org.apache.zookeeper:zookeeper'
    testImplementation 'org.apache.curator:curator-test'

    // DiffUtils
    implementation 'com.googlecode.java-diff-utils:diffutils'

    // jCommander
    optionalImplementation 'com.beust:jcommander'

    // jGit
    implementation 'org.eclipse.jgit:org.eclipse.jgit'
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch'

    // Micrometer
    implementation 'io.micrometer:micrometer-core'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Quartz
    implementation 'org.quartz-scheduler:quartz'

    // Mockito for mocking a Project
    jmh 'org.mockito:mockito-core'
    // Logging
    optionalImplementation 'ch.qos.logback:logback-classic'
    optionalImplementation 'org.slf4j:jul-to-slf4j'
}

def clientRelocations = [
        'ace-builds/src-min-noconflict/',
        'angular/angular.min.js',
        'angular-cache-buster/angular-cache-buster.js',
        'angular-cookies/angular-cookies.min.js',
        'angular-dynamic-locale/dist/tmhDynamicLocale.min.js',
        'angular-ui-ace/src/ui-ace.js',
        'angular-i18n',
        'angular-local-storage/dist/angular-local-storage.min.js',
        'angular-resource/angular-resource.min.js',
        'angular-sanitize/angular-sanitize.min.js',
        'angular-translate/dist/angular-translate.min.js',
        'angular-translate-loader-partial/angular-translate-loader-partial.min.js',
        'angular-translate-storage-cookie/angular-translate-storage-cookie.min.js',
        'angular-ui-ace/src/ui-ace.js',
        'angular-ui-bootstrap/dist/',
        'angular-ui-notification/dist/',
        'angular-ui-router/release/angular-ui-router.min.js',
        'bootstrap/dist/',
        'bootswatch/cerulean/bootstrap.min.css',
        'diff/dist/diff.min.js',
        'fast-json-patch/dist/json-patch-duplex.min.js',
        'jquery/dist/jquery.min.js',
        'json3/lib/json3.min.js',
        'moment/min/moment.min.js'
]

task copyClientDependencies {
    dependsOn tasks.npmInstall

    doLast {
        clientRelocations.each { location ->
            def paths = location.split('/')
            def folder = paths[0]
            copy {
                from "${project.projectDir}/node_modules/$location"
                into "${project.projectDir}/src/main/resources/webapp/vendor/$folder"
            }
        }
    }
}

task cleanClientDependencies(type: Delete) {
    delete "${project.projectDir}/src/main/resources/webapp/vendor"
}

task copyWebApp(type: Copy) {
    dependsOn ':server-auth:webapp:buildWebApp'

    def dst = "${project.ext.genSrcDir}/main/resources/auth-webapp";

    from project(':server-auth:webapp').file('build')
    into dst

    doFirst {
        delete(dst)
    }
}
tasks.sourcesJar.dependsOn(tasks.copyWebApp)
tasks.sourcesJar.dependsOn(tasks.versionProperties)
tasks.clean.dependsOn(tasks.cleanClientDependencies)
tasks.processResources.dependsOn(tasks.copyClientDependencies, tasks.copyWebApp)
