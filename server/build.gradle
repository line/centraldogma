buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.craigburke.gradle:client-dependencies:${managedVersions['com.craigburke.gradle:client-dependencies']}"
    }
}

apply plugin: 'com.craigburke.client-dependencies'

dependencies {
    implementation project(':common-legacy')

    // Armeria
    ['armeria', 'armeria-thrift0.9'].each {
        api "com.linecorp.armeria:$it"
    }
    testImplementation 'com.linecorp.armeria:armeria-testing-junit'

    // JSch
    implementation 'com.jcraft:jsch'

    // Caffeine
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // Curator & ZooKeeper
    implementation 'org.apache.curator:curator-recipes'
    implementation 'org.apache.zookeeper:zookeeper'
    testImplementation 'org.apache.curator:curator-test'

    // DiffUtils
    implementation 'com.googlecode.java-diff-utils:diffutils'

    // jCommander
    optionalImplementation 'com.beust:jcommander'

    // jGit
    implementation 'org.eclipse.jgit:org.eclipse.jgit'
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch'

    // Micrometer
    implementation 'io.micrometer:micrometer-core'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Quartz
    implementation 'org.quartz-scheduler:quartz'

    // Mockito for mocking a Project
    jmh 'org.mockito:mockito-core'
    // Logging
    optionalImplementation 'ch.qos.logback:logback-classic'
    optionalImplementation 'org.slf4j:jul-to-slf4j'
}

clientDependencies {
    installDir = "${project.projectDir}/src/main/resources/webapp/vendor"
    copyExcludes = ['**/Gruntfile.js', 'gulpfile.js', 'source/**', 'test', 'karma.conf.js', 'index.js']
    npm {
        'ace-builds'('1.2.5', from: 'src-min-noconflict')
        'angular'('1.5.8')
        'angular-cache-buster'('0.4.3')
        'angular-cookies'('1.5.8')
        'angular-dynamic-locale'('0.1.32')
        'angular-i18n'('1.5.8')
        'angular-local-storage'('0.5.0')
        'angular-resource'('1.5.8')
        'angular-sanitize'('1.5.8')
        'angular-translate'('2.13.0', transitive: false)
        'angular-translate-loader-partial'('2.13.0')
        'angular-translate-storage-cookie'('2.13.0')
        'angular-ui-ace'('0.2.3', from: 'src')
        'angular-ui-bootstrap'('2.2.0')
        'angular-ui-notification'('0.2.0')
        'angular-ui-router'('0.3.2')
        'bootstrap'('3.3.7')
        'bootswatch'('3.3.7')
        'diff'('3.0.1')
        'fast-json-patch'('1.1.1')
        'jquery'('2.2.4')
        'json3'('3.3.2', from: 'lib')
        'moment'('2.16.0', from: 'min')
    }
}

task copyWebApp(type: Copy) {
    dependsOn ':server-auth:webapp:buildWebApp'

    def dst = "${project.ext.genSrcDir}/main/resources/auth-webapp";

    from project(':server-auth:webapp').file('build')
    into dst

    doFirst {
        delete(dst)
    }
}

tasks.clean.dependsOn(tasks.clientClean)
tasks.processResources.dependsOn(tasks.clientInstall, tasks.copyWebApp)

