import java.nio.file.FileVisitOption
import java.nio.file.FileVisitResult
import java.nio.file.Files
import java.nio.file.SimpleFileVisitor

plugins {
    id 'com.moowork.node' version '1.3.1'
}

apply plugin: 'base'

tasks.yarn.doLast {
    // Delete the broken symlinks so that Gradle does not fail the build.
    // - https://github.com/gradle/gradle/issues/1365
    // Note that this is primarily due to a bug in Yarn 1.11+:
    // - https://github.com/yarnpkg/yarn/issues/6585
    // - https://github.com/yarnpkg/yarn/issues/6615
    // TODO(trustin): Remove the workaround once Yarn fixes yarnpkg/yarn#6585.

    def visitor = new SimpleFileVisitor<java.nio.file.Path>() {
        @Override
        FileVisitResult visitFileFailed(java.nio.file.Path file, IOException exc) throws IOException {
            logger.warn("Removing a broken symlink: ${file}")
            Files.delete(file)
            return FileVisitResult.CONTINUE
        }
    }

    Files.walkFileTree(
            project.file("${projectDir}/node_modules").toPath(),
            [FileVisitOption.FOLLOW_LINKS] as Set,
            Integer.MAX_VALUE,
            visitor)
}

task buildWebApp(type: YarnTask) {
    dependsOn 'yarn'
    args = ['run', 'build']
}

tasks.assemble.dependsOn tasks.buildWebApp

node {
    version = '10.16.0'
    yarnVersion = '1.17.3'
    download = true
}
