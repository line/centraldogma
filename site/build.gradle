import net.davidecavestro.gradle.jxr.JxrLog
import org.apache.maven.jxr.JXR

plugins {
    id 'base'
    alias libs.plugins.sphinx apply false
    alias libs.plugins.jxr
}

if (project.hasProperty("noSite")) {
    return
}

apply plugin: "kr.motd.sphinx"

def osdetector = project.rootProject.osdetector
def skipSphinx = osdetector.os == 'osx' && osdetector.arch == 'aarch_64'

sphinx {
    group = 'Documentation'
    description = 'Generates the Sphinx web site.'
    sourceDirectory "${project.projectDir}/src/sphinx"
    skip = skipSphinx
}

task xref(group: 'Documentation',
        description: 'Generates the source cross-reference.') {

    def outputDir = "${project.buildDir}/site/xref"
    def sourceDirs = projectsWithFlags('java', 'publish').inject([]) { srcDirs, project ->
        project.sourceSets.main.java.srcDirs.each {
            if (it.exists()) {
                srcDirs << it.path
            }
        }
        return srcDirs
    }

    inputs.files sourceDirs
    outputs.dir outputDir


    doLast {
        JXR jxr = new JXR()
        jxr.dest = outputDir
        jxr.inputEncoding = 'UTF-8'
        jxr.outputEncoding = 'UTF-8'
        jxr.log = new JxrLog(logger: logger)

        def title = "Central Dogma ${project.version} cross-reference"
        jxr.xref(sourceDirs, 'templates', title, title, rootProject.ext.copyrightFooter)
        ant.copy(file: "${project.projectDir}/src/xref/stylesheet.css", todir: jxr.dest)
    }
}

tasks.site {
    group = 'Documentation'
    description = 'Generates the project web site.'
    dependsOn xref
    dependsOn ':javadoc:javadoc'
}

tasks.assemble {
    dependsOn tasks.site
}

tasks.check.dependsOn tasks.sphinx
tasks.sphinx {
    dependsOn ':javadoc:javadoc'
}

tasks.build.dependsOn tasks.assemble
tasks.build.dependsOn tasks.check

def siteProject = project
rootProject.subprojects { prj ->
    siteProject.tasks.xref.dependsOn(prj.ext.getGenerateSourcesTask())
}
