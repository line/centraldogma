plugins {
    id 'com.github.blindpirate.gogradle' version '0.7.0'
}

golang {
    packagePath = 'github.com/line/centraldogma/client/go'

    goVersion = '1.9'
}

dependencies {
    golang {
        build 'github.com/urfave/cli@v1.19.1'
        // no tags or releases
        build 'github.com/bgentry/go-netrc#9fd32a8b3d3d3f9d43c341bfe098430e07609480'
        // no tags or releases
        build 'github.com/golang/crypto#7d9177d70076375b9a59c8fde23d52d9c4a7ecd5'
        // no tags or releases
        build 'github.com/BurntSushi/toml#8b58b6030fce084b58a61e2bc3fdf183d5881ab4'
    }
}

ext {
    goPath = System.env.GOPATH
    if (goPath == null) {
        goPath = "${project.projectDir}/.gogradle/project_gopath"
    }
    binDir = "${goPath}/bin"
}

// Gogradle puts several comman code check task into a check task. We need to explicitly specify this line
// to invoke the tasks. By default, it is out-of-the-box and depends on vet/fmt and cover.
build.dependsOn check

build {
    targetPlatform = ['linux-amd64', 'darwin-amd64', 'windows-amd64']

    go "build -o ${project.ext.binDir}" +
            '/dogma.${GOOS}-${GOARCH}${GOEXE} github.com/line/centraldogma/client/go/cmd/dogma'
    doLast {
        ant.chmod(file: "$project.ext.binDir" + '/dogma.windows-amd64.exe', perm: '644')
    }
}
