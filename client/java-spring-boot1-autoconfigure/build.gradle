dependencies {
    compile project(':client:java-armeria-legacy')
    compile 'javax.validation:validation-api'
    compile 'org.springframework.boot:spring-boot-starter:1.5.22.RELEASE'

    testCompile project(':client:java-armeria')
    testCompile 'junit:junit'
    testCompile 'org.springframework.boot:spring-boot-starter-test:1.5.22.RELEASE'
    testRuntime 'org.hibernate.validator:hibernate-validator'
}

// Use the sources from 'client-spring-boot-autoconfigure'.
// NB: We should not add these in the 'sourceSets' directive because that will make the directories mentioned
//     below are added to two projects and having a source directory with two output directories will confuse
//     the IDE.
def autoconfigureProjectDir = "${rootProject.projectDir}/client/java-spring-boot-autoconfigure"
tasks.compileJava.source "${autoconfigureProjectDir}/src/main/java"
tasks.processResources.from "${autoconfigureProjectDir}/src/main/resources"
tasks.compileTestJava.source "${autoconfigureProjectDir}/src/test/java", "${autoconfigureProjectDir}/gen-src/test/java"
tasks.processTestResources.from "${autoconfigureProjectDir}/src/test/resources"
tasks.sourceJar.from "${autoconfigureProjectDir}/src/main/java"
tasks.sourceJar.from "${autoconfigureProjectDir}/src/main/resources"
tasks.javadoc.source "${autoconfigureProjectDir}/src/main/java"

// Disable checkstyle because it's checked by 'client-spring-boot-autoconfigure'
tasks.checkstyleMain.onlyIf { false }
tasks.checkstyleTest.onlyIf { false }
